plugins {
    id 'java-library'
}

dependencies {

    api 'org.springframework.boot:spring-boot-dependencies:2.3.2.RELEASE'
    api 'org.springframework.cloud:spring-cloud-dependencies:Hoxton.SR6'
    api 'com.alibaba.cloud:spring-cloud-alibaba-dependencies:2.2.1.RELEASE'
    api 'io.micrometer:micrometer-registry-prometheus:1.5.2'
    api 'org.projectlombok:lombok:1.18.12'
    api 'com.alibaba:transmittable-thread-local:2.11.5'
    api 'net.logstash.logback:logstash-logback-encoder:6.4'
    api 'org.aspectj:aspectjweaver:1.9.5'
    api 'io.springfox:springfox-swagger2:2.9.2'
    api 'io.springfox:springfox-swagger-ui:2.9.2'
    api 'org.javassist:javassist:3.27.0-GA'
    api 'org.testcontainers:testcontainers:1.14.1'
    api 'org.awaitility:awaitility:3.0.0'
    api 'com.github.tomakehurst:wiremock-standalone:2.6.0'
    api 'org.apache.httpcomponents:fluent-hc:4.5.6'

}

publishing {
    repositories {
        maven {
            if(version.contains('SNAPSHOT')){
                url = repositoryMavenSnapshot
            }else{
                url = repositoryMavenRelease
            }
            credentials {
                username = "admin"
                password = "admin123"
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            groupId = "${project.group}"
            artifactId = "${project.name}"
            version = "${project.version}"

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST

                    name 'framework-dependency-management'
                    description 'Global thirdpart dependency management (BOM)'

                    dependencyManagement {
                        dependencies {
                            project.configurations.each { conf ->
                                conf.dependencies.each { dep ->
                                    dependency {
                                        groupId "${dep.group}"
                                        artifactId "${dep.name}"
                                        version "${dep.version}"
                                        if("${dep.name}".contains('dependencies')){
                                            type "pom"
                                            scope "import"
                                        }
                                    }
                                }
                            }
                        }
                    }
                    repositories {
                        repository {
                            id "nexus"
                            url "http://nexus.bigbang.internal/repository/maven-public/"
                            snapshots {
                                enabled true
                            }
                            releases {
                                enabled true
                            }
                        }
                    }
                }
            }
        }
    }
}